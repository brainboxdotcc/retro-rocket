MOUSE_UDP_PORT = 14501

UDPBIND "127.0.0.1", MOUSE_UDP_PORT

done = 0
spins = 0
REPEAT
  s = INPORT(&64)
  IF BITAND(s, 1) <> 0 AND BITAND(s, &20) <> 0 THEN
    discard = INPORT(&60)
  ELSE
    done = 1
  ENDIF
  spins = spins + 1
UNTIL spins > 64 OR done = 1

PROCmouse_send(&FF)
PROCmouse_send(&F6)
PROCmouse_send(&F4)

x = 0
y = 0
buttons = 0
DIM pkt,3
idx = 0

REPEAT
  b = FNread_mouse_byte()
  IF b >= 0 THEN
    IF idx = 0 AND BITAND(b, 8) = 0 THEN
        valid_first = 0
    ELSE
        valid_first = 1
    ENDIF
    IF valid_first = 1 THEN
      pkt(idx) = b
      idx = idx + 1

      IF idx = 3 THEN
        b0 = pkt(0)
        b1 = pkt(1)
        b2 = pkt(2)

        dx = b1
        dy = b2
        IF BITAND(b0, &10) <> 0 THEN
          dx = dx - 256
        ENDIF
        IF BITAND(b0, &20) <> 0 THEN
          dy = dy - 256
        ENDIF

        x = x + dx
        y = y - dy

        buttons = BITAND(b0, 7)

        idx = 0
      ENDIF
    ENDIF
  ENDIF

  PROCudp_handle_request
UNTIL FALSE

DEF PROCudp_handle_request
  buf$ = UDPREAD$(MOUSE_UDP_PORT)
  IF buf$ = "" THEN
    ENDPROC
  ENDIF

  from_ip$ = UDPLASTIP$
  from_port = UDPLASTSOURCEPORT

  IF buf$ = "GET" THEN
    resp$ = STR$(x) + " " + STR$(y) + " " + STR$(buttons)
    UDPWRITE from_ip$, MOUSE_UDP_PORT, from_port, resp$
  ENDIF
ENDPROC

DEF PROCwait_ibf_clear(max_spins)
  spins = 0
  REPEAT
    spins = spins + 1
  UNTIL BITAND(INPORT(&64), 2) = 0 OR spins > max_spins
ENDPROC

DEF FNread_mouse_byte
  s = INPORT(&64)
  IF BITAND(s, 1) = 0 THEN
    =-1
  ENDIF
  IF BITAND(s, &20) = 0 THEN
    =-1
  ENDIF
=INPORT(&60)

DEF PROCmouse_send(b)
  tries = 0
  ack = 0
  REPEAT
    PROCwait_ibf_clear(64)
    OUTPORT &64, &D4
    PROCwait_ibf_clear(64)
    OUTPORT &60, b
    ack = 0
    resend = 0
    spins = 0
    REPEAT
      r = FNread_mouse_byte()
      IF r = &FA THEN
        ack = 1
      ENDIF
      IF r = &FE THEN
        resend = 1
      ENDIF
      spins = spins + 1
    UNTIL spins > 128 OR ack = 1 OR resend = 1
    tries = tries + 1
  UNTIL tries > 3 OR ack = 1
ENDPROC
