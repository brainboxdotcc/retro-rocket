DEF PROCansi
	EDhistPtr = -1
ENDPROC

REM hide text cursor
DEF PROChideCursor
	PRINT CHR$(27);"[?25l";
ENDPROC

REM show text cursor
DEF PROCshowCursor
	PRINT CHR$(27);"[?25h";
ENDPROC

REM clear line from cursor to right
DEF PROCclearLineFromCursor
	PRINT CHR$(27) + "[0K";
ENDPROC

REM handle up key to recall through history
DEF PROCEDup
	IF EDhistPtr = -1 THEN ENDPROC
	EDPos = EDPos + 1
	IF EDPos = EDhistSize THEN EDPos = EDhistSize - 1
	EDin$ = EDhist$(EDPos)
	EDcurrentx = LEN(EDin$)
ENDPROC

REM handle down key to recall through history
DEF PROCEDdown
	IF EDhistPtr = -1 THEN ENDPROC
	EDPos = EDPos - 1
	IF EDPos < 0 THEN EDPos = 0
	EDin$ = EDhist$(EDPos)
	EDcurrentx = LEN(EDin$)
ENDPROC

REM add a line to the history (this is not automatic)
DEF PROCEdAddHistory(hist$)
	IF EDhistPtr = -1 THEN ENDPROC
	PUSH EDhist$,1
	EDhist$(1) = hist$
	EDhistPtr = EDhistPtr + 1
	IF EDhistPtr = EDhistSize THEN PROCEDgrow
ENDPROC

REM grow the history array to accomodate more history lines
DEF PROCEDgrow
	REDIM EDhist$, EDhistSize + 50
	EDhistSize = EDhistSize + 50
ENDPROC

REM handle left edit key
DEF PROCEDleft
	EDcurrentx = EDcurrentx - 1
	IF EDcurrentx < 0 THEN EDcurrentx = 0
ENDPROC

REM handle right edit key
DEF PROCEDright
	EDcurrentx = EDcurrentx + 1
	IF EDcurrentx < 0 THEN EDcurrentx = 0
ENDPROC

REM handle end edit key
DEF PROCEDend
	EDcurrentx = LEN(EDin$)
ENDPROC

REM handle home edit key
DEF PROCEDhome
	EDcurrentx = 0
ENDPROC

REM handle delete edit key
DEF PROCEDdelete
	EDlen = LEN(EDin$)
	IF EDcurrentx < EDlen THEN PROCEDdeleteRight
ENDPROC

REM handle backspace edit key
DEF PROCEDbackSpace
	cx = EDcurrentx
	IF cx > 0 THEN PROCEDdeleteLeft
ENDPROC

REM delete character to left of cursor, advance left
DEF PROCEDdeleteLeft
	EDstart$ = MID$(EDin$, 0, EDcurrentx - 1)
	EDend$ = MID$(EDin$, EDcurrentx, LEN(EDin$))
	EDin$ = EDstart$ + EDend$
	EDcurrentx = EDcurrentx - 1
ENDPROC

REM delete one character to the right
DEF PROCEDdeleteRight
	EDstart$ = MID$(EDin$, 0, EDcurrentx)
	EDend$ = MID$(EDin$, EDcurrentx + 1, LEN(EDin$))
	EDin$ = EDstart$ + EDend$
	EDlen = LEN(EDin$)
	IF EDcurrentx > EDlen THEN EDcurrentx = EDlen
ENDPROC

REM clear line from cursor to right
DEF PROCEDclearLineFromCursor
	PRINT CHR$(27) + "[0K";
ENDPROC

REM insert character into edit line up to TERMWIDTH -1
DEF PROCEDinsertChar(charInsert)
	IF (EDcurrentx + EDStartInputX) > (TERMWIDTH - 1) THEN ENDPROC
	EDstart$ = MID$(EDin$, 0, EDcurrentx)
	EDend$ = MID$(EDin$, EDcurrentx, LEN(EDin$))
	EDin$ = EDstart$ + CHR$(charInsert) + EDend$
	EDcurrentx = EDcurrentx + 1
	IF EDhistPtr = -1 THEN ENDPROC
	EDhist$(0) = EDin$
ENDPROC

REM process any incoming key into the edit buffer
DEF PROCEdRun
	KGET key$
	PROCEDprocessKey(ASC(key$))
ENDPROC

REM internal - Initialise internal history variables
DEF PROCEdInitInternalHistory
	DIM EDhist$, 50
	EDhistSize = 50
	EDhistPtr = 1
	EDPos = 0
ENDPROC

REM initialise line edit (history = TRUE to enable history, FALSE to disable history)
DEF PROCEdInit(history)
	IF history = TRUE THEN PROCEdInitInternalHistory
ENDPROC

REM start a line edit at the current cursor position, finished by pressing ENTER or ESC
DEF PROCEdStart
	EDin$ = ""
	EdEsc = FALSE
	EDcurrentx = 0
	EDPos = 0
	EDStartInputX = CURRENTX + 1
	EDStartInputY = CURRENTY + 1
	EdComplete = FALSE
ENDPROC

REM handle line enter
DEF PROCEDenter
	EdComplete = TRUE
ENDPROC

REM handle line abort (ESC) - clears line
DEF PROCEDabort
	EDin$ = ""
	CURSOR EDStartInputX, EDStartInputY
	PROCclearLineFromCursor
	EdComplete = TRUE
	EdEsc = TRUE
ENDPROC

REM get the line edit result
DEF FNEdResult$
=EDin$

REM return true if line edit was aborted (ESC)
DEF FNEdAborted
=EdEsc

REM Accept line input using line-edit and history
REM return the completed line in FNEdResult$()
DEF PROCEdLine
	PROCEdStart
	REPEAT
		PROCEdRun
	UNTIL EdComplete = TRUE
ENDPROC

REM handle line edit keypress
DEF PROCEDprocessKey(in)
	IF in = 13 THEN PROCEDenter
	IF in = 27 THEN PROCEDabort
	IF in = 250 THEN PROCEDup
	IF in = 251 THEN PROCEDdown
	IF in = 252 THEN PROCEDleft
	IF in = 253 THEN PROCEDright
	IF in = 249 THEN PROCEDend
	IF in = 254 THEN PROCEDhome
	IF in = 247 THEN PROCEDdelete
	IF in = 8 THEN PROCEDbackSpace
	IF (in < 128) AND (in > 31) THEN PROCEDinsertChar(in)
	PROChideCursor
	CURSOR EDStartInputX, EDStartInputY
	PROCclearLineFromCursor
	PRINT EDin$;
	CURSOR EDStartInputX + EDcurrentx, EDStartInputY
	PROCshowCursor
ENDPROC

